plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
}

group = 'com.github.weezard12'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly gradleApi()
    implementation("org.ow2.asm:asm:9.8")
    implementation("org.ow2.asm:asm-tree:9.8")
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    withJavadocJar()
    withSourcesJar()

    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(targetJavaVersion)
    } else {
        sourceCompatibility = targetJavaVersion.toString()
        targetCompatibility = targetJavaVersion.toString()
    }
    options.compilerArgs.add('-Xlint:-options')
}

tasks.withType(Javadoc).configureEach {
    options.addBooleanOption('Xdoclint:none', true)
    options.addStringOption('quiet', '-quiet')
}

gradlePlugin {
    plugins {
        endCrystalWmInjector {
            id = 'me.weezard12.endcrystalwm-injector'
            implementationClass = 'me.weezard12.endCrystalWM.gradle.EndCrystalWmInjectorPlugin'
            displayName = 'EndCrystalWM Injector'
            description = 'Injects EndCrystalWM bootstrap into plugin onEnable during jar build.'
        }
    }
}

publishing {
    publications.withType(MavenPublication).configureEach {
            pom {
                name = 'EndCrystalWM'
                description = 'A shadeable Paper/Bukkit watermark library with optional Gradle bytecode injector.'
            }
    }
}
