plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'com.github.weezard12'
version = '1.0.0'

repositories {
    mavenCentral()
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    withJavadocJar()
    withSourcesJar()

    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(targetJavaVersion)
    options.compilerArgs.add('-Xlint:-options')
}

tasks.withType(Javadoc).configureEach {
    options.addBooleanOption('Xdoclint:none', true)
    options.addStringOption('quiet', '-quiet')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'EndCrystalWM'
                description = 'A shadeable Paper/Bukkit watermark library.'
            }
        }
    }
}
